Multichar_Symbols

%<v%>
%<det%>
%<n%>
%<np%>
%<prn%>
%<adj%>
%<qst%>
%<num%>
%<adv%>
%<pr%>


%<tv%>
%<iv%>

%<pres%>
%<past%>
%<subj%>
%<punt%>

%<caus%>

%<itg%>

%<sg1%>
%<sg2%>
%<sg3%>
%<du1%>
%<pl1%>
%<pl2%>
%<pl3%>

%<in%>
%<ex%>

@U.AGR.SG1@
@U.AGR.SG2@
@U.AGR.SG3@
@U.AGR.DU1@
@U.AGR.PL1IN@
@U.AGR.PL1EX@
@U.AGR.PL2@
@U.AGR.PL3@

@D.AGR@

@U.TAM.PRES@
@U.TAM.PAST@
@U.TAM.SUBJ@
@U.TAM.IRRE@
@U.TAM.PUNT@
@U.TAM.PROG@

%{a%} ! a 0
%{A%} ! a e 
%{i%} ! i 0
%{ː%} ! a e i o u ü
%{ü%} ! ü a
%{I%} ! i ü
%{j%} ! j 0
%{P%} ! 0
%{R%} ! ⎈ 0

%{n%} ! n

%{V%} ! a ü o i e

LEXICON Root

Prefixes ;
Determinantes ;
Sustantivos ;
Adjetivos ;
Pronombres ;
Propios ; 
Adverbios ; 
Numeros ; 
Conjunciones ;
Preposiciones ;
Interyecciones ;
Símbolos ;
Miscellanea ;
Digits ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON K

%+a%<qst%>:%>%{A%} # ;
               # ;

LEXICON Prefixes

Accidentes ;
Posesivos ;
Estatus ;

LEXICON Estatus

%<stat%>:n%> AGR-STAT ;
%<punt%>%<stat%>:l%> AGR-STAT ;

LEXICON Posesivos 

! p.42

@U.AGR.SG1@%<px1sg%>:@U.AGR.SG1@xi%> Sustantivos ;
@U.AGR.SG1@%<px1sg%>:@U.AGR.SG1@xe%> Sustantivos ;
@U.AGR.SG1@%<px1sg%>:@U.AGR.SG1@sa%> Sustantivos ;
@U.AGR.SG2@%<px2sg%>:@U.AGR.SG2@mi%> Sustantivos ;
@U.AGR.SG3@%<px3sg%>:@U.AGR.SG3@mi%> Sustantivos ;
@U.AGR.SG2@%<px2sg%>:@U.AGR.SG2@i%> Sustantivos ;
@U.AGR.SG3@%<px3sg%>:@U.AGR.SG3@o%> Sustantivos ;
@U.AGR.PL1EX@%<px1pl%>%<ex%>:@U.AGR.PL1EX@xe%> Sustantivos ;
@U.AGR.PL1EX@%<px1pl%>%<ex%>:@U.AGR.PL1EX@xi%> Sustantivos ;
@U.AGR.PL1IN@%<px1pl%>%<in%>:@U.AGR.PL1IN@o%> Sustantivos ;
@U.AGR.PL1IN@%<px1pl%>%<in%>:@U.AGR.PL1IN@mi%> Sustantivos ;
@U.AGR.PL2@%<px2pl%>:@U.AGR.PL2@i%> Sustantivos ;
@U.AGR.PL3@%<px3pl%>:@U.AGR.PL3@mi%> Sustantivos ;
@U.AGR.PL3@%<px3pl%>:@U.AGR.PL3@o%> Sustantivos ;

LEXICON Accidentes 

@U.TAM.PUNT@%<punt%>:@U.TAM.PUNT@l%{a%}%> AGR-PUNT ;
@U.TAM.PAST@%<past%>:@U.TAM.PAST@t%> AGR-PAST ;
@U.TAM.PROG@%<prog%>:@U.TAM.PROG@te%{a%}%> AGR-PROG ;
@U.TAM.IRRE@%<irre%>:@U.TAM.IRRE@    AGR-IRRE ;
@U.TAM.SUBJ@%<subj%>:@U.TAM.SUBJ@    AGR-SUBJ ;
@U.TAM.PRES@%<pres%>:@U.TAM.PRES@    AGR-PRES ;

%<rel%>:na%> Verbos ; 
%<subst%>%<sg%>:ne%> Verbos ; 
%<subst%>%<pl%>:mo%{n%}%> Verbos ; 

LEXICON AGR-PUNT

@U.AGR.SG1@%<sg1%>:@U.AGR.SG1@na%> PASS ;
@U.AGR.SG1@%<sg1%>:@U.AGR.SG1@sa%> IV ;

@U.AGR.PL1EX@%<pl1%>%<ex%>:@U.AGR.PL1EX@na%> PASS ;
@U.AGR.PL1EX@%<pl1%>%<ex%>:@U.AGR.PL1EX@sa%> IV ;

@U.AGR.SG2@%<sg2%>:@U.AGR.SG2@er%> Verbos ;
@U.AGR.PL2@%<pl2%>:@U.AGR.PL2@er%> Verbos ;

@U.AGR.SG3@%<sg3%>:@U.AGR.SG3@a%> Verbos ;
@U.AGR.DU1@%<du1%>:@U.AGR.DU1@ma%> Verbos ;
@U.AGR.DU1@%<du1%>:@U.AGR.DU1@a%> Verbos ;
@U.AGR.PL1IN@%<pl1%>%<in%>:@U.AGR.PL1IN@ma%> Verbos ;
@U.AGR.PL3@%<pl3%>:@U.AGR.PL3@a%> Verbos ;
@U.AGR.PL3@%<pl3%>:@U.AGR.PL3@ma%> Verbos ;

LEXICON AGR-PROG

@U.AGR.SG1@%<sg1%>:@U.AGR.SG1@na%> PASS ;
@U.AGR.SG1@%<sg1%>:@U.AGR.SG1@sa%> IV ;

@U.AGR.PL1EX@%<pl1%>%<ex%>:@U.AGR.PL1EX@na%> PASS ;
@U.AGR.PL1EX@%<pl1%>%<ex%>:@U.AGR.PL1EX@sa%> IV ;

@U.AGR.SG2@%<sg2%>:@U.AGR.SG2@i%> Verbos ;
@U.AGR.PL2@%<pl2%>:@U.AGR.PL2@i%> Verbos ;

@U.AGR.SG3@%<sg3%>:@U.AGR.SG3@a%> Verbos ;
@U.AGR.DU1@%<du1%>:@U.AGR.DU1@a%> Verbos ;
@U.AGR.PL1IN@%<pl1%>%<in%>:@U.AGR.PL1IN@a%> Verbos ;
@U.AGR.PL3@%<pl3%>:@U.AGR.PL3@a%> Verbos ;


LEXICON AGR-STAT

@U.AGR.SG3@%<sg3%>:@U.AGR.SG3@a%> Stativos ;
@U.AGR.DU1@%<du1%>:@U.AGR.DU1@a%> Stativos ;
@U.AGR.PL1EX@%<pl1%>%<ex%>:@U.AGR.PL1EX@a%> Stativos ;
@U.AGR.PL1IN@%<pl1%>%<in%>:@U.AGR.PL1IN@a%> Stativos ;
@U.AGR.PL3@%<pl3%>:@U.AGR.PL3@a%> Stativos ;

@U.AGR.SG2@%<sg2%>:@U.AGR.SG2@er%> Stativos ;
@U.AGR.PL2@%<pl2%>:@U.AGR.PL2@er%> Stativos ;


LEXICON AGR-SG3-DU-PL13

@U.AGR.SG3@%<sg3%>:@U.AGR.SG3@a%> Verbos ;
@U.AGR.DU1@%<du1%>:@U.AGR.DU1@a%> Verbos ;
@U.AGR.PL1EX@%<pl1%>%<ex%>:@U.AGR.PL1EX@a%> Verbos ;
@U.AGR.PL1IN@%<pl1%>%<in%>:@U.AGR.PL1IN@a%> Verbos ;
@U.AGR.PL3@%<pl3%>:@U.AGR.PL3@a%> Verbos ;

LEXICON AGR-2

@U.AGR.SG2@%<sg2%>:@U.AGR.SG2@e%> Verbos ;
@U.AGR.PL2@%<pl2%>:@U.AGR.PL2@e%> Verbos ;


LEXICON AGR-IRRE

@U.AGR.SG1@%<dep%>%<sg1%>:@U.AGR.SG1@sa%>na%> Verbos ;
@U.AGR.SG1@%<dep%>%<sg1%>:@U.AGR.SG1@xi%>na%> Verbos ;
@U.AGR.SG1@%<dep%>%<sg1%>:@U.AGR.SG1@xe%>na%> Verbos ;
@U.AGR.SG2@%<dep%>%<sg2%>:@U.AGR.SG2@ap%>m%>i%> Verbos ;
@U.AGR.SG2@%<dep%>%<sg2%>:@U.AGR.SG2@ap%>m%>e%> Verbos ;
@U.AGR.SG3@%<dep%>%<sg3%>:@U.AGR.SG3@ap%>m%>a%> Verbos ;
@U.AGR.DU1@%<dep%>%<du1%>:@U.AGR.DU1@a%> Verbos ;
@U.AGR.DU1@%<dep%>%<du1%>:@U.AGR.DU1@ap%>m%>a%> Verbos ;
@U.AGR.PL1EX@%<dep%>%<pl1%>%<ex%>:@U.AGR.PL1EX@sa%>na%> Verbos ;
@U.AGR.PL1IN@%<pl1%>%<in%>:@U.AGR.PL1IN@ap%>m%>a%> Verbos ;
@U.AGR.PL2@%<dep%>%<pl2%>:@U.AGR.PL2@ap%>m%>i%> Verbos ;
@U.AGR.PL2@%<dep%>%<pl2%>:@U.AGR.PL2@ap%>m%>e%> Verbos ;
@U.AGR.PL3@%<dep%>%<pl3%>:@U.AGR.PL3@ap%>m%>a%> Verbos ;

LEXICON AGR-PRES

@U.AGR.SG1@%<sg1%>:@U.AGR.SG1@sa%> Verbos ;
@U.AGR.SG2@%<sg2%>:@U.AGR.SG2@i%> Verbos ;
@U.AGR.SG3@%<sg3%>:@U.AGR.SG3@a%> Verbos ;
@U.AGR.DU1@%<du1%>:@U.AGR.DU1@a%> Verbos ;
@U.AGR.PL1EX@%<pl1%>%<ex%>:@U.AGR.PL1EX@sa%> Verbos ;
@U.AGR.PL1IN@%<pl1%>%<in%>:@U.AGR.PL1IN@a%> Verbos ;
@U.AGR.PL2@%<pl2%>:@U.AGR.PL2@i%> Verbos ;
@U.AGR.PL3@%<pl3%>:@U.AGR.PL3@a%> Verbos ;

LEXICON AGR-PAST

@U.AGR.SG1@%<sg1%>:@U.AGR.SG1@a%> Verbos ;
@U.AGR.SG2@%<sg2%>:@U.AGR.SG2@i%> Verbos ;
@U.AGR.SG2@%<sg2%>:@U.AGR.SG2@e%> Verbos ;
@U.AGR.SG3@%<sg3%>:@U.AGR.SG3@a%> Verbos ;
@U.AGR.DU1@%<du1%>:@U.AGR.DU1@a%> Verbos ;
@U.AGR.PL1EX@%<pl1%>%<ex%>:@U.AGR.PL1EX@a%> Verbos ;
@U.AGR.PL1IN@%<pl1%>%<in%>:@U.AGR.PL1IN@a%> Verbos ;
@U.AGR.PL2@%<pl2%>:@U.AGR.PL2@i%> Verbos ;
@U.AGR.PL2@%<pl2%>:@U.AGR.PL2@e%> Verbos ;
@U.AGR.PL3@%<pl3%>:@U.AGR.PL3@a%> Verbos ;



LEXICON AGR-SUBJ

@U.AGR.SG1@%<sg1%>:@U.AGR.SG1@na%> Verbos ;
:m%>                               AGR-2;
:m%>                               AGR-SG3-DU-PL13;

LEXICON PASS 

      TV ;
!:%>%{j%} TV ;

LEXICON V-PERS

@U.AGR.SG2@:@U.AGR.SG2@%> # ;
@U.AGR.SG3@:@U.AGR.SG3@%> # ;
@U.AGR.DU1@:@U.AGR.DU1@%>%{V%}r # ;
@U.AGR.DU1@:@U.AGR.DU1@%>i%{V%}r # ;
@U.AGR.PL1IN@:@U.AGR.PL1IN@%>%{V%}%{V%}ts # ;
@U.AGR.PL1EX@:@U.AGR.PL1EX@%>%{V%}n # ;
@U.AGR.PL1EX@:@U.AGR.PL1EX@%>i%{V%}n # ;
@U.AGR.PL2@:@U.AGR.PL2@%>%{V%}n # ;
@U.AGR.PL2@:@U.AGR.PL2@%>i%{V%}n # ;
@U.AGR.PL3@:@U.AGR.PL3@%>%{V%}w # ;
@U.AGR.PL3@:@U.AGR.PL3@%>%{V%}j # ;

@U.AGR.SG3@%<refl%>:@U.AGR.SG3@%>ay # ;
@U.AGR.PL3@%<refl%>:@U.AGR.PL3@%>ay%>ej # ;

LEXICON V-SUF 

@U.TAM.PUNT@:@U.TAM.PUNT@ V-PERS ;
@U.TAM.PRES@:@U.TAM.PRES@ V-PERS ;
@U.TAM.PAST@:@U.TAM.PAST@ V-PERS ;
@U.TAM.PROG@:@U.TAM.PROG@ V-PERS ;
@U.TAM.SUBJ@:@U.TAM.SUBJ@ V-PERS ;
@U.TAM.IRRE@:@U.TAM.IRRE@ V-PERS ;

@U.AGR.SG1@@U.TAM.PRES@:@U.AGR.SG1@@U.TAM.PRES@%> # ;
@U.AGR.PL1EX@@U.TAM.PRES@:@U.AGR.PL1EX@@U.TAM.PRES@%>an # ;

@U.AGR.SG1@@U.TAM.PAST@:@U.AGR.SG1@@U.TAM.PAST@%>%{V%}s # ;
@U.AGR.PL1EX@@U.TAM.PAST@:@U.AGR.PL1EX@@U.TAM.PAST@%>asan # ;

@U.AGR.SG1@@U.TAM.SUBJ@:@U.AGR.SG1@@U.TAM.SUBJ@ # ;

@U.AGR.SG1@@U.TAM.IRRE@:@U.AGR.SG1@@U.TAM.IRRE@ # ;

@U.AGR.SG1@@U.TAM.PROG@:@U.AGR.SG1@@U.TAM.PROG@%> # ;

LEXICON V-TV

%<v%>%<tv%>: V-SUF ;
!%<v%>%<tv%>%<pass%>:%>%{P%} # ;

LEXICON V-IV

%<v%>%<iv%>: V-SUF ;
%<v%>%<iv%>%<caus%>:%>üüch V-SUF ;
%<v%>%<iv%>%<caus%>:%>iich V-SUF ;
%<v%>%<iv%>%<caus%>:%>eech V-SUF ;
%<v%>%<iv%>%<caus%>:%>aach V-SUF ;


LEXICON POSS 

@U.AGR.SG1@:@U.AGR.SG1@ K ; 
@U.AGR.SG2@:@U.AGR.SG2@ K ; 
@U.AGR.SG3@:@U.AGR.SG3@ K ; 
@U.AGR.DU1@:@U.AGR.DU1@%>iür K ; 
@U.AGR.PL1EX@:@U.AGR.PL1EX@%>iün K ; 
@U.AGR.PL1IN@:@U.AGR.PL1IN@%>iüts K ; 
@U.AGR.PL1IN@:@U.AGR.PL1IN@%>iiüts K ; 
@U.AGR.PL2@:@U.AGR.PL2@%>iün K ; 
@U.AGR.PL2@:@U.AGR.PL2@%>an K ; 
@U.AGR.PL3@:@U.AGR.PL3@%>üw K ; 
@U.AGR.PL3@:@U.AGR.PL3@%>iw K ; 

LEXICON N1 

@D.AGR@%<n%>:@D.AGR@ K ;
%<n%>: POSS ;
@U.AGR.SG3@%<n%>%<alien%>:@U.AGR.SG3@%>aran # ;

LEXICON N2

@D.AGR@%<n%>:@D.AGR@ K ;
%<n%>: POSS ;
@U.AGR.SG3@%<n%>%<alien%>:@U.AGR.SG3@%>aran # ;

LEXICON N3

@D.AGR@%<n%>:@D.AGR@ K ;
%<n%>: POSS ;
@D.AGR@%<n%>%<pl%>:@D.AGR@%>iw K ;
%<n%>%<pl%>:%>iw POSS ;

LEXICON N4

%<n%>: POSS ;

LEXICON N5

@D.AGR@%<n%>:@D.AGR@ K ;
@D.AGR@%<n%>%<pl%>:@D.AGR@%>s # ;
%<n%>: POSS ;

LEXICON N6

@D.AGR@%<n%>:@D.AGR@ K ;
@D.AGR@%<n%>%<pl%>:@D.AGR@%>es # ;
%<n%>: POSS ;


LEXICON NP-TOP

%<np%>%<top%>: # ;


LEXICON NP-ANT-F

%<np%>%<ant%>%<f%>: # ;


LEXICON NP-ANT-M 

%<np%>%<ant%>%<m%>: # ;

LEXICON A-STAT

%<adj%>: A-STAT-PERS ; 

LEXICON A-STAT-PERS

@U.AGR.SG1@:@U.AGR.SG1@%> # ;
@U.AGR.SG2@:@U.AGR.SG2@%> # ;
@U.AGR.SG3@:@U.AGR.SG3@%> # ;
@U.AGR.DU1@:@U.AGR.DU1@%>ar # ;
@U.AGR.PL1IN@:@U.AGR.PL1IN@%>%{ü%}%{ü%}ts # ;
@U.AGR.PL1EX@:@U.AGR.PL1EX@%>an # ;
@U.AGR.PL2@:@U.AGR.PL2@%>iün # ;
@U.AGR.PL3@:@U.AGR.PL3@%>%{I%}w # ;



LEXICON A 

%<adj%>: K ; 

LEXICON DET 

%<det%>: # ;

LEXICON NUM-NO

%<num%>%<c1%>:%>yky # ;
%<num%>%<c1%>:%>ik # ;
%<num%>%<c1%>:%>yk # ;
%<num%>%<c2%>:%>p # ;
%<num%>%<c3%>:%>ts # ;
%<num%>%<c4%>:%>mb # ;

LEXICON NUM-IJ

%<num%>%<c1%>:j%>ky%>aw # ;
%<num%>%<c1%>:j%>ki%>aw # ;
%<num%>%<c2%>:j%>p%>üw # ;
%<num%>%<c3%>:j%>ts%>üw # ;
%<num%>%<c4%>:%>üm # ;

LEXICON NUM-AR

%<num%>%<c1%>:ej # ;
%<num%>%<c2%>:oj%>p%>üw # ;
%<num%>%<c3%>:oj%>ts%>üw # ;
%<num%>%<c4%>:oo%>mb # ;

LEXICON NUM-GAJPIK

%<num%>%<c1%>:%>k # ;
%<num%>%<c2%>:ü%>p # ;
%<num%>%<c3%>:ü%>ts # ;



LEXICON NUM 

%<num%>: # ; 

LEXICON ADV 

%<adv%>: K ;

LEXICON ADV-ITG

%<adv%>%<itg%>: # ;

LEXICON PRON-POS

%<prn%>%<pos%>: POSS ;

LEXICON PRON-IND

%<prn%>%<ind%>: K ;

LEXICON PRON-DEM

%<prn%>%<dem%>: K ;

LEXICON PRON-REL

%<prn%>%<rel%>: K ;

LEXICON PRON-ITG 

%<prn%>%<itg%>: # ;

LEXICON CS

%<cnjsub%>: # ;

LEXICON CC 

%<cnjcoo%>: # ; 

LEXICON PREP 

%<pr%>: # ;

LEXICON INTJ

%<ij%>: # ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Conjunciones

mbich:mbich CC ; ! "pero"
pero:pero CC ; ! "pero"
pero:pere CC ; ! "pero"
y:y CC ; ! "y"


kos:kos CS ; ! "porque"
wüx:wüx CS ; ! "cuando"
para:para CS ; ! "para"
masey:masey CS ; ! "aunque"
sitiül:sitiül CS ; ! "si"
tengwüy:tengwüy CS ; ! "si no"
ndot:ndot CS ; ! "si"

LEXICON Preposiciones 

tiül:tiül PREP ; ! "en"

! COMPROBAR
at:at PREP ; ! "como" 
wüx:wüx PREP ; ! "sobre" 
naag:naag PREP ; ! "con"
nikan:nikan PREP ; ! "al lado de"

hasta:hasta PREP ;  ! "hasta"
por:por PREP ;  ! "por"
para:para PREP ;  ! "para"

! Contracciones

tiül%<pr%>%+:ti%> Sustantivos ; 
tiül%<pr%>%+:te%> Posesivos ; 

LEXICON Pronombres

xike%<prn%>%<pers%>%<sg1%>%<emph%>:xike # ; 
ike%<prn%>%<pers%>%<sg2%>%<emph%>:ike # ; 
ikora%<prn%>%<pers%>%<du1%>%<emph%>:ikora # ; 
ikootsa%<prn%>%<pers%>%<pl1%>%<ex%>%<emph%>:xikona # ; 
ikootsa%<prn%>%<pers%>%<pl1%>%<in%>%<emph%>:ikootsa # ; 
ikona%<prn%>%<pers%>%<pl2%>%<emph%>:ikona # ; 

xike%<prn%>%<pers%>%<sg1%>:xik # ; 
ike%<prn%>%<pers%>%<sg2%>:ik # ; 
ikor%<prn%>%<pers%>%<du1%>:ikor # ; 
ikoots%<prn%>%<pers%>%<pl1%>%<ex%>:xikon # ; 
ikoots%<prn%>%<pers%>%<pl1%>%<in%>:ikoots # ; 
ikon%<prn%>%<pers%>%<pl2%>:ikon # ; 

nej%<prn%>%<pers%>%<sg3%>:nej # ; 
nejiw%<prn%>%<pers%>%<pl3%>:nejiw # ; 

jan:jan PRON-ITG ; ! "quién"
jan:jane PRON-ITG ; ! "quién" ! +A
kwane:kwane PRON-ITG ; ! "qué"
kwa:kwa PRON-ITG ; ! "qué"

leaw:leaw PRON-REL ; ! "lo que"

áagan:áagan PRON-DEM ; ! "éste"
ajküw:ajküw PRON-DEM ; ! "éste"

kwajantanej:kwajantanej PRON-IND ; ! "cualquier cosa"

aljane:aljane PRON-IND ; ! "alguien"
nikwajind:nikwajind PRON-IND ; ! "nada"
nejingind:nejingind PRON-IND ; ! "ninguno, nadie"
jangantanej:jangantanej PRON-IND ; ! "cualquiera, quienquiera"
aleaik:aleaik PRON-IND ; ! "alguna cosa, algo"
ngünantanej:ngünantanej PRON-IND ; ! "cualquier cosa"


LEXICON Determinantes

aaga:aaga DET ; ! "este"
aaga:a DET ; ! ""
aaga:aag DET ; ! ""
aaga:áag DET ; ! ""
aaga:e DET ; ! ""

ngün:ngün DET ; ! "cuál"


LEXICON Numeros 

no:no NUM-NO ; ! "uno"
ij:i NUM-IJ ; ! "dos"
ar:ar NUM-AR ; ! "tres"

pikiw:pikiw NUM ; ! "cuatro"
akokiaw:akokiaw NUM ; ! "cinco"
anaíw:anaíw NUM ; ! "seis"
ayaíw:ayaíw NUM ; ! "siete"
ojpeaküw:ojpeaküw NUM ; ! "ocho"
ojkiyej:ojkiyej NUM ; ! "nueve"
gajpowüw:gajpowüw NUM ; ! "diez"
gajpowüw:gajpawüw NUM ; ! "diez"

gajpanoik:gajpano NUM-NO ; ! "once"
gajpik:gajpi NUM-GAJPIK ; ! "doce"
gajpar:gajpar NUM ; ! "trece"
gajpopeik:gajpopeik NUM ; ! "catorce"
gajpokoik:gajpokoik NUM ; ! "quince"
gajponüy:gajponüy NUM ; ! "dieciseis"
gajpoyay:gajpoyay NUM ; ! "diecisiete"
gajpopeak:gajpopeak NUM ; ! "dieciocho"
gajpokiy:gajpokiy NUM ; ! "diecinueve"

nimiow:nimiow NUM ; ! "veinte"
miow:miow NUM ; ! "veinte"

LEXICON Sustantivos 

aneay:neay PRON-POS ; ! "suyo"
okweaj:kweaj PRON-POS ; ! "suyo"

!kweaj:kweaj N1 ; ! "cosa"
!meajts:meajts N1 ; ! "???"

iüm:iüm N1 ; ! "puerta"
nden:nden N1 ; ! "casa"
niüng:niüng N1 ; ! "casa"
niiüg:niiüg N1 ; ! "ojo"
teat:teat N3 ; ! "padre"
teaats:tea%{ː%}ts N1 ; ! "sacerdote"
naxey:naxey N1 ; ! "señor"
yow:yow N1 ; ! "agua"
pil:pil N1 ; ! "pozo"
kiek:kiek N1 ; ! "pájaro"
küet:küet N1 ; ! "pescado"
taag:ta%{ː%}g N1 ; ! "piel"
xor:xor N1 ; ! "olla"
kambaj:kambaj N1 ; ! "pueblo"
korrül:korrül N1 ; ! "corral"
tomiün:tomiün N1 ; ! "dinero, moneda"
pet:pet N1 ; ! "perro"
mal:mal N1 ; ! "cabeza"
najiüt:najiüt N1 ; ! "trabajo"
%<sg%>najiüt%<n%>:ne%>najiüt # ; ! "autoridades"
%<pl%>najiüt%<n%>:mo%>najiüt # ; ! "autoridades"
iüt:iüt N1 ; ! "tierra"
xiül:xiül N1 ; ! "árbol"
monandeow:monandeow N1 ; ! "enfermedad"
plas:plas N1 ; ! "mercado"
kwal:kwal N1 ; ! "hijo"
müm:müm N1 ; ! "madre, abuela"
nüt:nüt N1 ; ! "nombre"
larraar:larra%{ː%}r N1 ; ! "calor"
nine:nine N1 ; ! "niño"
ombas:ombas N1 ; ! "cuerpo"
tiiüd:tiiüd N1 ; ! "camino"
nimal:nimal N1 ; ! "animal"
dios:dios N1 ; ! "Dios"
lam:lam N1 ; ! "río"
nendeajndeaj:nendeajndeaj N1 ; ! "basura"
nipilan:nipilan N1 ; ! "gente"

kawüy:kawüy N1 ; ! "caballo"
monxey:monxey N1 ; ! "hombre"
piedra:piedra N1 ; ! "piedra"
iünd:iünd N1 ; ! "aire"
sap:sap N1 ; ! "borrego, algodón"
mats:mats N1 ; ! "cama"
onij:onij N1 ; ! "carne"
opang:opang N1 ; ! "cáscara"
miüs:miüs N1 ; ! "gato"
aop:aop N1 ; ! "hoja"
omb:omb N1 ; ! "huevo"
üs:üs N1 ; ! "maíz"

onik:onik N1 ; ! "voz"
kiej:kiej N1 ; ! "sangre"
ndooch:ndooch N1 ; ! "cicatriz"
soen:soen N1 ; ! "canto"
püüig:püüig N1 ; ! "rama"
jael:jael N1 ; ! "tela"
neat:neat N1 ; ! "año"
kwüch:kwüch N1 ; ! "gemelos"
sow:sow N1 ; ! "puerco"
najlok:najlok N1 ; ! "grano"
jüm:jüm N1 ; ! "lagarto"
wear:wear N1 ; ! "pájaro"
win:win N1 ; ! "tortuga"
wiiüd:wiiüd N1 ; ! "arena"
ndek:ndek N1 ; ! "mar"
mbas:mbas N1 ; ! "lado"
loy:loy N1 ; ! "mariano"
naleaing:naleaing N1 ; ! "verdad"
nangaj:nangaj N1 ; ! "bendición"

kwalaats:kwala%{ː%}ts N1 ; ! "familia"

nerraar:nerra%{ː%}r N1 ; ! "calor"

! partes de cuerpo: con -aran (alienable)

leaj:leaj N2 ; ! "pierna" 
wix:wix N2 ; ! "mano" 

mbeay:mbeay N1 ; ! "boca"

! Siempre poseído?

leaj:leaj N1 ; ! "pierna"  

chep:chep N4 ; ! "muela"
meaats:mea%{ː%}ts N4 ; ! "corazón"

kalüy:kalüy N1 ; ! "norte"
kawak:kawak N1 ; ! "norte"
nonüt:nonüt N1 ; ! "oriente"
noleat:noleat N1 ; ! "poniente"

monson:monson N1 ; ! "música"

seman:seman N1 ; ! "semana"
vin:vin N1 ; ! "vino"
vecin:vecin N1 ; ! "vecino"

espíritu:espíritu N1 ; ! "espíritu"

ángel:ángel N6 ; ! "ángel"
nación:nación N5 ; ! "nación"
vuelta:vuelta N5 ; ! "vuelta"
amigo:amigo N5 ; ! "amigo"
cielo:cielo N5 ; ! "cielo"
vinagre:vinagre N5 ; ! "vinagre"
esponja:esponja N5 ; ! "esponja"
cuerpo:cuerpo N5 ; ! "cuerpo"
cuerpo:kwerpo N5 ; ! "cuerpo"
casa:casa N5 ; ! "casa"
casa:kasa N5 ; ! "casa"
templo:templo N5 ; ! "templo"
maestro:maestro N5 ; ! "maestro"
fariseo:fariseo N5 ; ! "fariseo"
levadura:levadura N5 ; ! "levadura"
judío:judío N5 ; ! "judío"
griego:griego N5 ; ! "griego"
gracia:gracia N5 ; ! "gracia"
sinagoga:sinagoga N5 ; ! "sinagoga"
soldado:soldado N5 ; ! "soldado"
oro:oro N5 ; ! "oro"
sello:sello N5 ; ! "sello"
trono:trono N5 ; ! "trono"
uva:uva N5 ; ! "uva"
castigo:kastigo N5 ; ! "castigo"
castigo:castigo N5 ; ! "castigo"
apóstol:apóstol N6 ; ! "apóstol"
pan:pan N6 ; ! "apóstol"
cruz:kruz N6 ; ! "cruz"
cruz:cruz N6 ; ! "cruz"

ajiüng:ajiüng N1 ; ! "recitación, lección"
ajponch:ajponch N1 ; ! "las olas grandes"
ajtsaj:ajtsaj N1 ; ! "masa"
ajüch:ajüch N1 ; ! "su deuda, su cuenta"
ajüy:ajüy N1 ; ! "tejido; telar"
akiiüb:akiiüb N1 ; ! "compañero"
akwalaats:akwala%{ː%}ts N1 ; ! "sus parientes"
alkiriw:alkiriw N1 ; ! "por poco, otro poco"
anch:anch N1 ; ! "$ojera, pereza"
andeak:andeak N1 ; ! "habla, discurso, mensaje"
andiig:andi%{ː%}g N1 ; ! "abalorio, collar"
anendeoog:anendeo%{ː%}g N1 ; ! "eco"
anganeow:anganeow N1 ; ! "bebida, alimentos líquidos"
angot:angot N1 ; ! "su baton"
aniüng:aniüng N1 ; ! "casa, hogar"
apaj:apaj N1 ; ! "su calzado, sus huaraches"
apak:apak N1 ; ! "fuerza"
apal:apal N1 ; ! "silla para gobernar"
apal:apal N1 ; ! "tapa"
apix:apix N1 ; ! "ropa, traje, vestido, cobija"
apop:apop N1 ; ! "espuma"
as:as N1 ; ! "elote"
asoet:asoet N1 ; ! "pecado"
ateam:ateam N1 ; ! "terremoto, temblor"
awan:awan N1 ; ! "municipio"
biümb:biümb N1 ; ! "lumbre, fuego; in&#34;erno, purgatorio"
chech:chech N1 ; ! "insecto hemíptero"
chech:chech N1 ; ! "lunar, mancha pequeña y morena de piel"
ijchiür:ijchiür N1 ; ! "lluvia"
ik:ik N1 ; ! "hígado"
ind:ind N1 ; ! "instrumento musical de viento"
ind:ind N1 ; ! "gulabere"
ipak:ipak N1 ; ! "apoyo"
jaw:jaw N1 ; ! "mentón, barbilla"
jeng:jeng N1 ; ! "$echa, gancho para cortar palmas"
jiw:jiw N1 ; ! "teta, seno, pecho"
jow:jow N1 ; ! "hamaca"
jüy:jüy N1 ; ! "consuegro, consuegra"
kaaw:ka%{ː%}w N1 ; ! "mes, luna"
kambajiüt:kambajiüt N1 ; ! "gente"
kang:kang N1 ; ! "vulva, testículo"
kieed:kie%{ː%}d N1 ; ! "un tipo de camisa de lana hecha a mano"
kiniük:kiniük N1 ; ! "sal"
kiripite:kiripite N1 ; ! "poquito"
kit:kit N1 ; ! "gal ina"
koet:koet N1 ; ! "cerco, muro"
koex:koex N1 ; ! "inválido, cojo"
koj:koj N1 ; ! "hermano o hermana mayor"
kos:kos N1 ; ! "paludismo"
kow:kow N1 ; ! "metate"
kün:kün N1 ; ! "abeja"
küx:küx N1 ; ! "baúl, cajón"
küy:küy N1 ; ! "salina, lugar bajo donde tierra contiene sal"
kwalsoj:kwalsoj N1 ; ! "abrojo, ortiga"
lan:lan N1 ; ! "rompevientos, protección dviento"
las:las N1 ; ! "mecate"
leaj:leaj N1 ; ! "pierna"  
leaj:leaj N1 ; ! "pierna"  
lil:lil N1 ; ! "lagartija escamoso hocico negro"
lil:lil N1 ; ! "las escamas de pescado"
lol:lol N1 ; ! "pozo"
loliüt:loliüt N1 ; ! "hoyo"
lop:lop N1 ; ! "ciruedesecada"
lop:lop N1 ; ! "hambre"
lüw:lüw N1 ; ! "tigre, jaguar"
machat:machat N1 ; ! "machete"
mal:mal N1 ; ! "cabeza"
mal:mal N1 ; ! "cabeza"
manchiük:manchiük N1 ; ! "&#34;erro, hierro; cárcel; ri$e, arma"
mbaj:mbaj N1 ; ! "$or"
mbaw:mbaw N1 ; ! "baúl"
mbeay:mbeay N1 ; ! "boca"
mbeay:mbeay N1 ; ! "boca"
mbeay:mbeay N1 ; ! "mazorca"
mbeay:mbeay N1 ; ! "orilla, playa"
mbójan:mbójan N1 ; ! "sonido cuando se golpea una cosa hueca o cuando una cosa se"
mbol:mbol N1 ; ! "miedo"
meay:meay N1 ; ! "pampanilla, taparrabo"
mikiamb:mikiamb N1 ; ! "su mano iziquierda"
mil¹:mil N1 ; ! "lisa"
mil²:mil N1 ; ! "mil"
mimüman:mimüman N1 ; ! "madre desposo, suegra dmujer"
minaj:minaj N1 ; ! "animal más grande entre sus companeros, animal que"
minatang:minatang N1 ; ! "su líder, su jefe"
minaw:minaw N1 ; ! "resto"
minaw:minaw N1 ; ! "sobrante de; resto de"
mindiw:mindiw N1 ; ! "sombra"
mineataran:mineataran N1 ; ! "su edad"
minench:minench N1 ; ! "su novio"
mintajaran:mintajaran N1 ; ! "esposa, mujer"
minüt:minüt N1 ; ! "nombre"
minüteran:minüteran N1 ; ! "su nombre"
minüx:minüx N1 ; ! "su novia; su hija"
miokwats:miokwats N1 ; ! "suegro o suegra desposo; yerno"
misoy:misoy N1 ; ! "hez, sedimiento, bagazo, desechos"
miteaats:mitea%{ː%}ts N1 ; ! "sacerdote, cura"
miteatiün:miteatiün N1 ; ! "suegro de mujer"
miwiiüd:miwiiüd N1 ; ! "cirueagria, ciruelo de fruta agria, roja,"
monajneaj:monajneaj N1 ; ! "buen carácter"
monapaküy:monapaküy N1 ; ! "salud"
monbayan:monbayan N1 ; ! "susto"
mondeak:mondeak N1 ; ! "discusión"
mongich:mongich N1 ; ! "los chamacos, los chicos"
monind:monind N1 ; ! "los músicos"
monjiüng:monjiüng N1 ; ! "danza, baile, los danzantes"
monjiünts:monjiünts N1 ; ! "llanto, tristeza, dolor"
monküy:monküy N1 ; ! "pleito"
monteok:monteok N1 ; ! "rayo"
montsoj:montsoj N1 ; ! "las batallas, los con$ictos, las guerras, los jugadores, el"
montsünd:montsünd N1 ; ! "los músicos"
monüx:monüx N1 ; ! "muchachas"
monxing:monxing N1 ; ! "vergüenza"
mow:mow N1 ; ! "chapulín"
müx:müx N1 ; ! "canoa, barco, cayuco"
naj:naj N1 ; ! "los propóleos"
najchow:najchow N1 ; ! "carrizo"
najkiol:najkiol N1 ; ! "ocote"
najlip:najlip N1 ; ! "relámpago"
najndot:najndot N1 ; ! "polvo"
najtaj:najtaj N1 ; ! "hembra; mujer, señora"
najüy:najüy N1 ; ! "que vive, que queda; caminante, uno que va"
nakwalaran:nakwalaran N1 ; ! "hijo"
nalil:nalil N1 ; ! "sudor; salpullido"
naliw:naliw N1 ; ! "criatura"
namix:namix N1 ; ! "sencil o, cambio, suelto"
namümaran:namümaran N1 ; ! "madre"
nandeow:nandeow N1 ; ! "muerto, cadáver"
nanderak:nanderak N1 ; ! "dicho; palabra, plática"
nangan:nangan N1 ; ! "dulce"
nangoraad:nangora%{ː%}d N1 ; ! "bastón que muestra autoridad"
nangün:nangün N1 ; ! "borracho"
nanojaran:nanojaran N1 ; ! "esposa, mujer"
napeweran:napeweran N1 ; ! "nuera"
napixeran:napixeran N1 ; ! "ropa, cobija"
napüik:napüik N1 ; ! "calzado, huarache"
napüp:napüp N1 ; ! "alacrán"
nasop:nasop N1 ; ! "humo, vapor"
natajtaj:natajtaj N1 ; ! "anciana, viejita"
natsaramb:natsaramb N1 ; ! "comida, comestible"
natsoj:natsoj N1 ; ! "peleador, jugador"
natsünderan:natsünderan N1 ; ! "instrumental musical"
naw:naw N1 ; ! "originario, es de"
nawaag:nawa%{ː%}g N1 ; ! "seco"
nawiig:nawi%{ː%}g N1 ; ! "papel; libro"
naxiül:naxiül N1 ; ! "milpa, maíz"
naxoraad:naxora%{ː%}d N1 ; ! "lo escondido"
nchep:nchep N1 ; ! "chiquigüite"
ncherrek:ncherrek N1 ; ! "brisa, viento dsur"
nchiiüts:nchiiüts N1 ; ! "red"
nchiül:nchiül N1 ; ! "langosta"
ndar:ndar N1 ; ! "capa delgada de grasa o sangre sobre agua"
ndeoj:ndeoj N1 ; ! "huérfano"
ndeoog:ndeo%{ː%}g N1 ; ! "comején"
ndeor:ndeor N1 ; ! "barro, lodo"
ndet:ndet N1 ; ! "boca de oro"
ndiük:ndiük N1 ; ! "aguja"
ndiük:ndiük N1 ; ! "culebra, serpiente, víbora; lombriz"
ndiw:ndiw N1 ; ! "pez cuatro ojos"
ndójwüx:ndójwüx N1 ; ! "última vez, último día"
ndok:ndok N1 ; ! "atarraya, red"
ndorrop:ndorrop N1 ; ! "un hoyo algo profundo, barranco"
neech:ne%{ː%}ch N1 ; ! "donador, que da"
need:ne%{ː%}d N1 ; ! "ladrón"
nejaw:nejaw N1 ; ! "testigo"
nejiür:nejiür N1 ; ! "guardia, velador, vigilante; rico"
nejüchay:nejüchay N1 ; ! "deudor"
nejüyich:nejüyich N1 ; ! "chofer, que conduce, piloto"
nekiaach:nekia%{ː%}ch N1 ; ! "maestro, que enseña"
nekixkix:nekixkix N1 ; ! "disentería"
nekoex:nekoex N1 ; ! "zopo, inválido"
nekoy:nekoy N1 ; ! "dolor, herida; tétanos"
nemal:nemal N1 ; ! "que carga"
nembeol:nembeol N1 ; ! "ayudante"
nembiy:nembiy N1 ; ! "que mata"
nembolooch:nembolooch N1 ; ! "aparición, visión; duende"
nenajiüt:nenajiüt N1 ; ! "que tiene autoridad"
nench:nench N1 ; ! "muchacho, jóven"
nendeak:nendeak N1 ; ! "voz, que habla"
nendüy:nendüy N1 ; ! "curandero, sabio"
neneay:neneay N1 ; ! "dueño"
nengalüy:nengalüy N1 ; ! "comprador"
nenüjpeay:nenüjpeay N1 ; ! "comerciante, vendedor, allagente"
nenüüb:nenü%{ː%}b N1 ; ! "vendedor"
nepaküüch:nepakü%{ː%}ch N1 ; ! "que lo despierta"
nepepep:nepepep N1 ; ! "brisa, aire fresco"
nepiür:nepiür N1 ; ! "sembrador, agricultor"
nerrombüy:nerrombüy N1 ; ! "glotón"
nesapüy:nesapüy N1 ; ! "instrumento con que se agarra algo"
nesomb:nesomb N1 ; ! "ciego"
nesomüy:nesomüy N1 ; ! "hechicero, brujo"
nesüet:nesüet N1 ; ! "que adivina"
net:net N1 ; ! "que come"
netsaag:netsa%{ː%}g N1 ; ! "manojo; nudo"
netüneay:netüneay N1 ; ! "limosnero, mendigo"
newinch:newinch N1 ; ! "que envuelve, envolvedero"
newün:newün N1 ; ! "salvador"
ngan:ngan N1 ; ! "cántaro"
ngex:ngex N1 ; ! "tórtogemidora"
nichech:nichech N1 ; ! "su ofrenda; ofrenda"
niiüg:niiüg N1 ; ! "ojo"
nimeech:nime%{ː%}ch N1 ; ! "diablo, Satanás"
nin:nin N1 ; ! "jején, moyote"
nine:nine N1 ; ! "niño"
nipeaxay:nipeaxay N1 ; ! "medidor, pesa, la"
nipüch:nipüch N1 ; ! "pala"
nisoet:nisoet N1 ; ! "delito, pecado"
nit:nit N1 ; ! "palma"
nitil:nitil N1 ; ! "clavo grande"
nitomb:nitomb N1 ; ! "incensario"
niw:niw N1 ; ! "nigua"
nüeteran:nüeteran N1 ; ! "comida, alimento"
nüt:nüt N1 ; ! "nombre"
nüt:nüt N1 ; ! "nombre"
nüt:nüt N1 ; ! "fecha, día"
nüx:nüx N1 ; ! "muchacha"
oik:oik N1 ; ! "nube, nubes"
okas:okas N1 ; ! "estrella; las estrellas"
olaag:ola%{ː%}g N1 ; ! "oído; oreja"
olaagaran:olaagaran N1 ; ! "oído; oreja"
olaats:ola%{ː%}ts N1 ; ! "hueso"
olam:olam N1 ; ! "caña de azúcar"
oleaj:oleaj N1 ; ! "pata; huella; pisada"
oleajaran:oleajaran N1 ; ! "pie, pierna, huella, pisada"
olüik:olüik N1 ; ! "diente"
olüikeran:olüikeran N1 ; ! "los dientes"
olümb:olümb N1 ; ! "ala"
omal:omal N1 ; ! "punta"
omalaran:omalaran N1 ; ! "cabeza"
ombasaran:ombasaran N1 ; ! "cuerpo"
ombeat:ombeat N1 ; ! "precio"
ombeay:ombeay N1 ; ! "boca"
ombeayiiüts:ombeayiiüts N1 ; ! "nuestra idioma, o sea huave"
ombiüm:ombiüm N1 ; ! "casa; nido"
omeaats:omea%{ː%}ts N1 ; ! "interior"
omeaatsaran:omeaatsaran N1 ; ! "corazón; vientre, las entrañas, abdomen, el"
ondeats:ondeats N1 ; ! "pelo, cabo"
ongwiiüts:ongwiiüts N1 ; ! "noche"
oniiüg:oniiüg N1 ; ! "ojo"
oniiügaran:oniiügaran N1 ; ! "ojo"
oniw:oniw N1 ; ! "lengua"
op:op N1 ; ! "zacate; hoja; roble"
opang:opang N1 ; ! "cáscara"
os:os N1 ; ! "maíz"
osaab:osa%{ː%}b N1 ; ! "semilla"
otüengeran:otüengeran N1 ; ! "abdomen, vientre, barriga"
owixeran:owixeran N1 ; ! "brazo"
oxet:oxet N1 ; ! "ancho"
oxiich:oxi%{ː%}ch N1 ; ! "raíz"
oxing:oxing N1 ; ! "punta, &#34;lo"
oxiüt:oxiüt N1 ; ! "tumba, sepulcro"
palangan:palangan N1 ; ! "vasija, cazuela"
pang:pang N1 ; ! "jicalpextle"
par:par N1 ; ! "raja dtalón por resequedad"
peat:peat N1 ; ! "selva, monte"
peats:peats N1 ; ! "tortilla"
pemb:pemb N1 ; ! "calabaza, bule"
pinawan:pinawan N1 ; ! "mitad, centro"
poch:poch N1 ; ! "palabra, verbo, frase, mensaje"
poj:poj N1 ; ! "tortugo rojo, sabanera, tortuga terrestre"
pojniün:pojniün N1 ; ! "oscuridad, las tinieblas"
pom:pom N1 ; ! "copal, incienso"
ponong:ponong N1 ; ! "montón"
popox:popox N1 ; ! "espuma"
por:por N1 ; ! "tacazonte"
potwit:potwit N1 ; ! "zopilote común"
pow:pow N1 ; ! "horno"
püjchiün:püjchiün N1 ; ! "llano, campo, extensión; aire libre"
püy:püy N1 ; ! "chal, pañuelo, rebozo; toalla"
ran:ran N1 ; ! "luz"
reech:re%{ː%}ch N1 ; ! "avaro, mezquino, tacaño"
rünch:rünch N1 ; ! "rancho, rastrojo, corral"
saab:sa%{ː%}b N1 ; ! "los testículos"
sampüy:sampüy N1 ; ! "coyote"
sats:sats N1 ; ! "espina"
soex:soex N1 ; ! "hierba, pasto, zacate, forraje; leña"
sokol:sokol N1 ; ! "rincón"
somb:somb N1 ; ! "ciego, tapanco"
soots:soots N1 ; ! "bigote, barba"
soy:soy N1 ; ! "sedimento"
süüig:süüig N1 ; ! "medicina, remedio"
tap:tap N1 ; ! "alga, lama en laguna"
teaats:teaats N1 ; ! "sacerdote"
teants:teants N1 ; ! "cabra, chivo"
tej:tej N1 ; ! "un especie de caracol forma de tornilla, un caracol marino chico"
tek:tek N1 ; ! "petate"
tet:tet N1 ; ! "pito, silbato"
tey:tey N1 ; ! "rostro, cara; máscara"
tiük:tiük N1 ; ! "cerro"
tongoy:tongoy N1 ; ! "curva"
tsak:tsak N1 ; ! "guanacaste"
tsongol:tsongol N1 ; ! "racimo"
üech:üech N1 ; ! "hacha"
wajchiük:wajchiük N1 ; ! "desierto, un lugar despoblado"
wajtsats:wajtsats N1 ; ! "las zarzas"
waküx:waküx N1 ; ! "ganado"
walang:walang N1 ; ! "bola, terrón"
warraj:warraj N1 ; ! "un hueco"
weak:weak N1 ; ! "cuerno, cacho"
wiiüd:wiiüd N1 ; ! "arena"
wiül:wiül N1 ; ! "rabo, cola"
wiür:wiür N1 ; ! "zanate, clarinero"
wün:wün N1 ; ! "pejesapo"
wün:wün N1 ; ! "pez sapo, miracielo sapo"
xan:xan N1 ; ! "señor, don"
xar:xar N1 ; ! "jarra, jarro"
xeech:xe%{ː%}ch N1 ; ! "viejo, abuelo, anciano"
xiüm:xiüm N1 ; ! "chapulín verde"
xiüt:xiüt N1 ; ! "mecahilo, cordón; vena"
xix:xix N1 ; ! "mosquito, mosco chico"

chiig:chi%{ː%}g N1 ; ! "hermano menor"

LEXICON Propios 

Teon:Teon NP-ANT-M ; ! "Antonio"


Jesús:Jesús NP-ANT-M ; ! "Jesús"
Cristo:Cristo NP-ANT-M ; ! "Cristo"
Pedro:Pedro NP-ANT-M ; ! "Pedro"
Juan:Juan NP-ANT-M ; ! "Juan"
Mateo:Mateo NP-ANT-M ; ! "Mateo"
Moisés:Moisés NP-ANT-M ; ! "Moisés"
Pablo:Pablo NP-ANT-M ; ! "Pablo"
Jesucristo:Jesukristo NP-ANT-M ; ! "Jesukristo"
Jesucristo:Jesucristo NP-ANT-M ; ! "Jesucristo"
Abraham:Abraham NP-ANT-M ; ! "Abraham"
Herodes:Herodes NP-ANT-M ; ! "Herodes"
Pilato:Pilato NP-ANT-M ; ! "Pilato"
David:David NP-ANT-M ; ! "David"
Judas:Judas NP-ANT-M ; ! "Judas"
Felipe:Felipe NP-ANT-M ; ! "Felipe"
Jakobo:Jakobo NP-ANT-M ; ! "Jakobo"
Jakob:Jakob NP-ANT-M ; ! "Jakob"
Silas:Silas NP-ANT-M ; ! "Silas"
Tito:Tito NP-ANT-M ; ! "Tito"
Esteban:Esteban NP-ANT-M ; ! "Esteban"
Melkisedek:Melkisedek NP-ANT-M ; ! "Melkisedek"
Agripa:Agripa NP-ANT-M ; ! "Agripa"
Félix:Félix NP-ANT-M ; ! "Félix"
Markos:Markos NP-ANT-M ; ! "Markos"
Akila:Akila NP-ANT-M ; ! "Akila"
Nikodemo:Nikodemo NP-ANT-M ; ! "Nikodemo"
Filipos:Filipos NP-ANT-M ; ! "Filipos"
Gayo:Gayo NP-ANT-M ; ! "Gayo"
Aristarko:Aristarko NP-ANT-M ; ! "Aristarko"
Alejandro:Alejandro NP-ANT-M ; ! "Alejandro"
Melki:Melki NP-ANT-M ; ! "Melki"
Lot:Lot NP-ANT-M ; ! "Lot"
Fares:Fares NP-ANT-M ; ! "Fares"
Aram:Aram NP-ANT-M ; ! "Aram"
Lukas:Lukas NP-ANT-M ; ! "Lukas"
Justo:Justo NP-ANT-M ; ! "Justo"
Erasto:Erasto NP-ANT-M ; ! "Erasto"
Epafras:Epafras NP-ANT-M ; ! "Epafras"
Demas:Demas NP-ANT-M ; ! "Demas"
Serug:Serug NP-ANT-M ; ! "Serug"
Sadok:Sadok NP-ANT-M ; ! "Sadok"
Rufo:Rufo NP-ANT-M ; ! "Rufo"
Resa:Resa NP-ANT-M ; ! "Resa"
Neri:Neri NP-ANT-M ; ! "Neri"
Lamek:Lamek NP-ANT-M ; ! "Lamek"
Josafat:Josafat NP-ANT-M ; ! "Josafat"
Jorim:Jorim NP-ANT-M ; ! "Jorim"
Joram:Joram NP-ANT-M ; ! "Joram"
Filipo:Filipo NP-ANT-M ; ! "Filipo"

Jerusalén:Jerusalén NP-TOP ; ! "Jerusalén"
Egipto:Egipto NP-TOP ; ! "Egipto"
Roma:Roma NP-TOP ; ! "Roma"
Asia:Asia NP-TOP ; ! "Asia"
Festo:Festo NP-TOP ; ! "Festo"
Damasco:Damasco NP-TOP ; ! "Damasco"
Tiro:Tiro NP-TOP ; ! "Tiro"
Acaya:Acaya NP-TOP ; ! "Acaya"
Sodoma:Sodoma NP-TOP ; ! "Sodoma"
Korinto:Korinto NP-TOP ; ! "Korinto"
Chipre:Chipre NP-TOP ; ! "Chipre"
Efeso:Efeso NP-TOP ; ! "Efeso"
Listra:Listra NP-TOP ; ! "Listra"
Hades:Hades NP-TOP ; ! "Hades"
Atenas:Atenas NP-TOP ; ! "Atenas"
Tarso:Tarso NP-TOP ; ! "Tarso"
Gomorra:Gomorra NP-TOP ; ! "Gomorra"
Derbe:Derbe NP-TOP ; ! "Derbe"
Lida:Lida NP-TOP ; ! "Lida"
Esrom:Esrom NP-TOP ; ! "Esrom"
Perge:Perge NP-TOP ; ! "Perge"
Mileto:Mileto NP-TOP ; ! "Mileto"
Genesaret:Genesaret NP-TOP ; ! "Genesaret"
Sikem:Sikem NP-TOP ; ! "Sikem"
Pafos:Pafos NP-TOP ; ! "Pafos"
España:España NP-TOP ; ! "España"
Esmirna:Esmirna NP-TOP ; ! "Esmirna"

María:María NP-ANT-F ; ! "María"
Marta:Marta NP-ANT-F ; ! "Marta"
Magdalena:Magdalena NP-ANT-F ; ! "Magdalena"
Sara:Sara NP-ANT-F ; ! "Sara"
Dorcas:Dorcas NP-ANT-F ; ! "Dorcas"
Rebeka:Rebeka NP-ANT-F ; ! "Rebeka"
Eva:Eva NP-ANT-F ; ! "Eva"
Susana:Susana NP-ANT-F ; ! "Susana"
Rakel:Rakel NP-ANT-F ; ! "Rakel"
Ninfas:Ninfas NP-ANT-F ; ! "Ninfas"
Drusila:Drusila NP-ANT-F ; ! "Drusila"
Ana:Ana NP-ANT-F ; ! "Ana"

Simón:Simón NP-ANT-M ; ! "Simón"
José:José NP-ANT-M ; ! "José"
Timoteo:Timoteo NP-ANT-M ; ! "Timoteo"
Bernabé:Bernabé NP-ANT-M ; ! "Bernabé"
Saulo:Saulo NP-ANT-M ; ! "Saulo"
Satanás:Satanás NP-ANT-M ; ! "Satanás"
Elías:Elías NP-ANT-M ; ! "Elías"
César:César NP-ANT-M ; ! "César"
Isaías:Isaías NP-ANT-M ; ! "Isaías"
Lázaro:Lázaro NP-ANT-M ; ! "Lázaro"
Isaak:Isaak NP-ANT-M ; ! "Isaak"
Zakarías:Zakarías NP-ANT-M ; ! "Zakarías"

Galilea:Galilea NP-TOP ; ! "Galilea"
Israel:Israel NP-TOP ; ! "Israel"
Judea:Judea NP-TOP ; ! "Judea"
Nazaret:Nazaret NP-TOP ; ! "Nazaret"
Makedonia:Makedonia NP-TOP ; ! "Makedonia"
Antioquía:Antioquía NP-TOP ; ! "Antioquía"
Samaria:Samaria NP-TOP ; ! "Samaria"
Kesarea:Kesarea NP-TOP ; ! "Kesarea"


LEXICON Adjetivos 

nine:nine A ; ! "chico, pequeño"
miünkiaj:miünkiaj A ; ! "lejos"
najneaj:najneaj A ; ! "bueno"
ndeanche:ndeanche A ; ! "pinche"

maril:maril A ; ! "amarillo"
tekech:tekech A ; ! "corto"
tirip:tirip A ; ! "ancho de terrenos"
oxtet:oxtet A ; ! "ancho de telas"
jayats:jayats A ; ! "nuevo"
rran:rran A ; ! "blanco"
pyets:pyets A ; ! "amarillo"
karakol:karakol A ; ! "morado"
asul:asul A ; ! "azul"
perex:perex A ; ! "tostado"

alinop:alinop A ; ! "otro"
alinoik:alinoik A ; ! "otro"
pálwüx:pálwüx A ; ! "otro, aparte"
palpálwüx:palpálwüx A ; ! ""
meáwan:meáwan A ; ! "todo"
pal:pal A ; ! "todo"
alakas:alakas A ; ! "algunos más"
akas:akas A ; ! "algunos"
palǘyan:palǘyan A ; ! "puro"
tigüy:tigüy A ; ! "poco"
tegüy:tegüy A ; ! "poco"
xeyay:xeyay A ; ! "mucho"
xowüy:xowüy A ; ! "mucho"

alngün:alngün A ; ! "algunos"
merrey:merrey A ; ! "mero"

matnej:matnej A ; ! "igual, mismo"

nembeat:nembeat A ; ! "caro"

netam:netam A ; ! "necesario"

nomb:nomb A ; ! "primero"

LEXICON Stativos

xip:xip A-STAT ; ! "gordo"
dam:dam A-STAT ; ! "grande"
kants:kants A-STAT ; ! "rojo"
waak:waak A-STAT ; ! "seco"
pat:pat A-STAT ; ! "bravo"
pak:pak A-STAT ; ! "fuerte, duro"

LEXICON Adverbios 

ngiane:ngiane ADV-ITG ; ! "dónde"
ngüt:ngüt ADV-ITG ; ! "cuándo"
ngow:ngow ADV-ITG ; ! "cuándo"
neol:neol ADV-ITG ; ! "por qué"
ngineay:ngineay ADV-ITG ; ! "cómo"

aweaag:aweaag ADV ; ! "con él/ella" !!! FIXME: Mo SS aweaag [ a-w′a:g ] con él, con ella│with him, with her
anaag:anaag ADV ; ! Mo SS anaag [ a-na:g ] con él, con ella│with him, with her

ayaj:ayaj ADV ; ! "allá" !!! FIXME: Mo SS aag ayaj [ a:g a-yah ] ése│that one

kawüx:kawüx ADV ; ! "arriba"

nóikian:nóikian ADV ; ! "juntamente"

tanaámb:tanaámb ADV ; ! "continuamente"

nde:nde ADV ; ! "no"
ngo:ngo ADV ; ! "no"
ngom:ngom ADV ; ! "no"
ngwüy:ngwüy ADV ; ! "no"

nomb:nomb ADV ; ! "al principio"

oxep:oxep ADV ; ! "mañana"
tim:tim ADV ; ! "ayer"
nganüy:nganüy ADV ; ! "ahora"
kondom:kondom ADV ; ! "después"
tepood:tepood ADV ; ! "anoche"
tilüy:tilüy ADV ; ! "antes"
ndoj:ndoj ADV ; ! "después"

aliün:aliün ADV ; ! "todavía"
tengial:tengial ADV ; ! "está" ! auxiliar para presente progresivo

nganaw:nganaw ADV ; ! "todavía"
nganüy:nganüy ADV ; ! "ahorita"

kamüm:kamüm ADV ; ! "entonces"

ningüy:ningüy ADV ; ! "aquí

xeyay:xeyay ADV ; ! "bastante"
xowüy:xowüy ADV ; ! "muchísimo"

aton:aton ADV ; ! "también"
atan:atan ADV ; ! "también" 
atan:átan ADV ; ! "también" 
alinomb:alinomb ADV ; ! "otra vez"

atnej:atnej ADV ; ! "así"
atkiaj:atkiaj ADV ; ! "así"

miün:miün ADV ; ! "solamente"

kam:kam ADV ; ! ""
kiaj:kiaj ADV ; ! "allí, entonces"
kiüj:kiüj ADV ; ! ""

! Mo SS aag agüy [ a:g a-gɨy ] ésto│this one
aiün:aiün ADV ; ! ""
agüy:agüy ADV ; ! ""
ayaj:ayaj ADV ; ! ""

tiüt:tiüt ADV ; ! "abajo"

kon:kon ADV ; ! "tal vez"

pares:pares ADV ; ! "por eso"
pues:pues ADV ; ! "pues"
más:más ADV ; ! "más"

LEXICON Miscellanea

al%<cop%>%<loc%>:al # ; 
al%<cop%>%<loc%>%+wüx%<pr%>:al%>wüx K ; 
al%<cop%>%<loc%>%+tiül%<pr%>:al%>tiül K ; 
al%<cop%>%<loc%>%+kiaj%<adv%>:al%>kiaj K ; 
al%<cop%>%<loc%>%+tiüt%<adv%>:al%>tiüt K ; 
al%<cop%>%<loc%>%+tiül%<pr%>%+:al%>te%> Posesivos ; 

chük%<evid%>:chük # ;
che%<evid%>:che # ;

! Son partículas o participios?
nasoik%<part%>:nasoik # ; ! Mo SS nasoik part [ na-sok′ ] llamado; dicho, le fue dicho
alndom%<prog%>:alndom # ; ! Mo SS alndom part cont [ al′-nd-o-m ] está terminado, después│to be "nished; later
ndot%<part%>:ndot # ; ! Mo SS ndot conj; part [ nd-o-t ] ¿ qué tal si ... ? si; pudo saber│what if, if; could (knew how to)
nenüt%<part%>:nenüt # ; ! Mo SS nenüt part llamado, se llama│named, to have the name of

%<pres%>%<sg3%>saj%<v%>%<tv%>:aj # ;

LEXICON Verbos

TV ;
IV ;

LEXICON TV

üüch:üüch V-TV ; ! "dar"

rang:rang V-TV ; ! "hacer"
kooch:ko%{ː%}ch%{i%} V-TV ; ! "sacar"
wün:wün V-TV ; ! "sacar"
lik:lik%{i%} V-TV ; ! "regañar"
ndaab:nda%{ː%}b V-TV ; ! "quemar"
jaw:jaw V-TV ; ! "ver"
ngiay:ngiay V-TV ; ! "saber"
ndiüm:ndiüm V-TV ; ! "querer"
saj:saj V-TV ; ! "decir"
piüng:piüng V-TV ; ! "decir"
soyky:soyky V-TV ; ! "decir"
yak:yak V-TV ; ! "echar"
xaing:xaing V-TV ; ! "levantar"
xom:xom V-TV ; ! "encontrar"
omb:omb V-TV ; ! "poner"
tepeay:tepeay V-TV ; ! "saludar"
kweat:kweat V-TV ; ! "dejar"
sap:sap V-TV ; ! "agarrar"
jiür:jiür V-TV ; ! "tener"
pants:pants V-TV ; ! "freir"
ngal:ngal V-TV ; ! "comprar"
pow:pow V-TV ; ! "hornear"
nganyow:nganyow V-TV ; ! "tomar"
tep:tep V-TV ; ! "subir"
nüüp:nü%{ː%}p V-TV ; ! "comprar"
jier:jier V-TV ; ! "hacer"

yamb:yamb V-TV ; ! "buscar"

ndeak:ndeak V-TV ; ! "hablar" 
ndeak:ndyak V-TV ; ! "hablar" 
tsamb:ts%{R%}amb V-TV ; ! "soltar"

jmbaj:jmbaj V-TV ; ! "romper"

ndiem:ndiem V-TV ; ! "querer"
üüt:ü%{ː%}t V-TV ; ! "comer"

kül:kül V-TV ; ! "esperar"
mongoch:mongoch V-TV ; ! "traducir, trasladar"

mbeol:mbeol V-TV ; ! "ayudar"

ndok:ndok V-TV ; ! "pescar"
mbiy:mbiy V-TV ; ! "golpear, matar"
jamiün:jamiün V-TV ; ! "traer"


char:char V-TV ; ! "lo encima"
cheed:che%{ː%}d V-TV ; ! "lo desata"
chej:chej V-TV ; ! "lo pide regalado; lo prohija"
chen:chen V-TV ; ! "lo enciende; lo prende; lo remoja "
chey:chey V-TV ; ! "lo sorbe"
chipich:chipich V-TV ; ! "lo llena; lo harta"
chiüt:chiüt V-TV ; ! "lo rompe"
iük:iük V-TV ; ! "lo aguanta; le dura"
iünd:iünd V-TV ; ! "lo aguanta; lo ayuna"
jan:jan V-TV ; ! "lo trae "
jants:jants V-TV ; ! "lo lava"
jarrich:jarrich V-TV ; ! "lo derriba"
jel:jel V-TV ; ! "lo arranca "
jenchjench:jenchjench V-TV ; ! "lo raya suavemente "
jend:jend V-TV ; ! "lo limpia "
jep:jep V-TV ; ! "lo corta"
jimb:jimb V-TV ; ! "lo barre"
jow:jow V-TV ; ! "se lo dice"
joy:joy V-TV ; ! "lo lleva "
jüikich:jüikich V-TV ; ! "lo registra"
kachiich:kachiich V-TV ; ! "lo remoja"
kaj:kaj V-TV ; ! "lo desentierra"
kancham:kancham V-TV ; ! "lo remienda"
kanchich:kanchich V-TV ; ! "lo junta"
kiaach:kiaach V-TV ; ! "lo enseña"
kiand:kiand V-TV ; ! "lo cubre"
kiiw:ki%{ː%}w V-TV ; ! "lo parte o quiebra en dos pedazos"
kind:kind V-TV ; ! "lo enfría"
kolot:kolot V-TV ; ! "lo despedaza; lo taja; lo troza "
kwarreech:kwarreech V-TV ; ! "lo hace correr"
kwiaach:kwiaach V-TV ; ! "lo machuca"
laj:laj V-TV ; ! "lo traga"
lal:lal V-TV ; ! "lo cuela "
langlang:langlang V-TV ; ! "lo menea"
lats:lats V-TV ; ! "tiende en línea horizontal"
leaaig:leaaig V-TV ; ! "abre"
leaj:leaj V-TV ; ! "la acarrea"
lech:lech V-TV ; ! "lo torea"
lech:lech V-TV ; ! "lo cuece "
liünd:liünd V-TV ; ! "lo aprieta"
lokooch:lokooch V-TV ; ! "lo enloquece"
lomb:lomb V-TV ; ! "lo alaba"
lomboch:lomboch V-TV ; ! "lo para"
lood:lo%{ː%}d V-TV ; ! "lo rompe; lo agujerea"
lop:lop V-TV ; ! "lo remoja"
lox:lox V-TV ; ! "lo tira"
lüeliich:lüeliich V-TV ; ! "lo hace volar"
lüp:lüp V-TV ; ! "lo lame"
mal:mal V-TV ; ! "lo carga en la espalda "
mbaag:mbaag V-TV ; ! "lo reparte"
mbel:mbel V-TV ; ! "lo enciende"
mbich:mbich V-TV ; ! "lo termina"
mbitik:mbitik V-TV ; ! "lo carga en el hombro"
mbiül:mbiül V-TV ; ! "lo enrolla "
mbolooch:mbolooch V-TV ; ! "lo espanta"
melich:melich V-TV ; ! "lo mete"
miüküch:miüküch V-TV ; ! "lo baja"
montsots:montsots V-TV ; ! "cierra "
moog:mo%{ː%}g V-TV ; ! "lo pone boca abajo"
naw:naw V-TV ; ! "le sobra"
naw:naw V-TV ; ! "le sobra; lo ahorra"
nchiül:nchiül V-TV ; ! "lo machuca"
nchom:nchom V-TV ; ! "lo pinta; lo mancha"
ndaküw:ndaküw V-TV ; ! "lo espía"
ndal:ndal V-TV ; ! "lo bate"
ndal:ndal V-TV ; ! "lo pica"
ndalal:ndalal V-TV ; ! "da vueltas"
ndarriich:ndarriich V-TV ; ! "lo borra"
nden:nden V-TV ; ! "lo sombrea"
ndendiich:ndendiich V-TV ; ! "lo cura"
ndik:ndik V-TV ; ! "lo extiende"
ndondoch:ndondoch V-TV ; ! "lo termina"
ndooig:ndooig V-TV ; ! "lo rompe; lo corta; lo cruza"
ndrooch:ndrooch V-TV ; ! "lo gasta"
ndüüb:ndü%{ː%}b V-TV ; ! "lo sigue"
ndüy:ndüy V-TV ; ! "está acostumbrado; lo cura"
neam:neam V-TV ; ! "lo a&#34;rma"
need:ne%{ː%}d V-TV ; ! "lo roba"
neok:neok V-TV ; ! "tiene vergüenza de ver"
netet:netet V-TV ; ! "lo jala"
ngoch:ngoch V-TV ; ! "lo encuentra"
ngüy:ngüy V-TV ; ! "lo paga"
not:not V-TV ; ! "lo jala; lo absorbe; lo capta; gana premio"
nüüb:nü%{ː%}b V-TV ; ! "lo vende"
oel:oel V-TV ; ! "lo amarra"
ol:ol V-TV ; ! "lo mezcla"
onts:onts V-TV ; ! "lo excreta"
paag:paag V-TV ; ! "lo acuesta boca arriba"
palüch:palüch V-TV ; ! "lo completa"
peat:peat V-TV ; ! "lo trenza; lo teje "
peax:peax V-TV ; ! "lo mide"
pech:pech V-TV ; ! "lo anuda "
pech:pech V-TV ; ! "lo pone "
peech:peech V-TV ; ! "lo tapa"
peed:pe%{ː%}d V-TV ; ! "lo corta "
peik:peik V-TV ; ! "lo abraza"
pej:pej V-TV ; ! "lo lava un poco"
piow:piow V-TV ; ! "lo saca "
piür:piür V-TV ; ! "lo siembra"
pooch:pooch V-TV ; ! "lo extiende"
pots:pots V-TV ; ! "lo carga "
püch:püch V-TV ; ! "lo echa "
rend:rend V-TV ; ! "lo cuelga "
ripip:ripip V-TV ; ! "lo esparce"
rreeg:rre%{ː%}g V-TV ; ! "lo toca"
rriiüd:rriiüd V-TV ; ! "lo escoge"
rroob:rro%{ː%}b V-TV ; ! "lo escarba"
samb:samb V-TV ; ! "lo toca"
sarriich:sarriich V-TV ; ! "lo dispersa"
sojnong:sojnong V-TV ; ! "lo concentra"
sok:sok V-TV ; ! "lo mete"
sol:sol V-TV ; ! "le mete algo para mantenerlo rigido"
sooig:sooig V-TV ; ! "lo viste"
sooig:sooig V-TV ; ! "lo agarra "
soond:soond V-TV ; ! "lo quita; lo desarma"
taad:taad V-TV ; ! "lo limpia"
taag:taag V-TV ; ! "lo destripa abriéndolo bien"
taag:taag V-TV ; ! "lo hace "
teaaig:teaaig V-TV ; ! "lo extiende"
teap:teap V-TV ; ! "lo mueve el viento "
tech:tech V-TV ; ! "lo palanquea; lo empuja "
tendeay:tendeay V-TV ; ! "lo enfría"
teng:teng V-TV ; ! "lo vacía"
teonts:teonts V-TV ; ! "escupe"
teots:teots V-TV ; ! "lo recuerda"
teots:teots V-TV ; ! "lo recuerda"
teow:teow V-TV ; ! "lo cuenta"
teow:teow V-TV ; ! "lo riega"
tepich:tepich V-TV ; ! "lo sube"
textexíichan:textexíichan V-TV ; ! "lo pudre"
tiib:ti%{ː%}b V-TV ; ! "lo señala"
til:til V-TV ; ! "lo apuñala"
tong:tong V-TV ; ! "lo vacía"
tool:to%{ː%}l V-TV ; ! "le pone boca arriba"
tox:tox V-TV ; ! "lo raspar"
tsaag:tsaag V-TV ; ! "lo amarra"
tsambiich:tsambiich V-TV ; ! "lo suelta"
tsants:tsants V-TV ; ! "lo recalienta poco"
tsoel:tsoel V-TV ; ! "lo punza"
tsopoch:tsopoch V-TV ; ! "lo mancha"
tsoriich:tsoriich V-TV ; ! "lo devuelve"
tsünd:tsünd V-TV ; ! "lo toca "
tüch:tüch V-TV ; ! "lo alcanza"
tün:tün V-TV ; ! "lo pide"
üet:üet V-TV ; ! "lo come"
wichwich:wichwich V-TV ; ! "lo mece; lo menea"
wiig:wi%{ː%}g V-TV ; ! "lo desgrana "
wikiach:wikiach V-TV ; ! "da a luz"
winch:winch V-TV ; ! "lo enrolla; lo enreda"
wind:wind V-TV ; ! "lo estira"
wit:wit V-TV ; ! "lo recoge"
witich:witich V-TV ; ! "lo levanta "
wiül:wiül V-TV ; ! "lo alimenta"
wün:wün V-TV ; ! "sacar"
xejken:xejken V-TV ; ! "lo apaga"
xejmbeach:xejmbeach V-TV ; ! "lo baña"
xejneng:xejneng V-TV ; ! "lo amontona"
xek:xek V-TV ; ! "lo entremete"
xol:xol V-TV ; ! "lo molesta"
xot:xot V-TV ; ! "lo esconde"
yaag:yaag V-TV ; ! "lo siente"
yar:yar V-TV ; ! "lo agarrar; lo trae; se apropia"
pak:pak V-TV ; ! "???"
mong:mong V-TV ; ! "gatear"

LEXICON IV

mong:mong V-IV ; ! "gatear"
ndeow:ndeow V-IV ; ! "morir"
tich:tich V-IV ; ! "llegar"
kül:kül V-IV ; ! "quedar"
mb:mb V-IV ; ! "ir"
meay:meay V-IV ; ! "dormir"
peay:peay V-IV ; ! "llegar"
w:w V-IV ; ! "subir"
ndilil:ndilil V-IV ; ! "regresar"
xeep:xe%{ː%}p V-IV ; ! "bañarse"
tang:tang V-IV ; ! "crecer"

jtsor:jtsor V-IV ; ! "regresar"
jtsop:jtsop V-IV ; ! "llegar"
jrrok:jrrok V-IV ; ! "nadar"
jküy:jküy V-IV ; ! "enojarse"
jiün:jiün V-IV ; ! "bailar"
jchiky:jchiky V-IV ; ! "saltar"

chip:chip V-IV ; ! ""

jlüy:jlüy V-IV ; ! "hay" !!! FIXME

chech:chech V-IV ; ! "mama"
cheech:cheech V-IV ; ! "chi$a "
chich:chich V-IV ; ! "se descompone"
dam:dam V-IV ; ! "crece"
damdam:damdam V-IV ; ! "son grandes"
iüm:iüm V-IV ; ! "gime"
iün:iün V-IV ; ! "viene"
jal:jal V-IV ; ! "es hondo"
jchik:jchik V-IV ; ! "brinca"
jiünts:jiünts V-IV ; ! "llora; chirría"
jlang:jlang V-IV ; ! "se mueve"
jlok:jlok V-IV ; ! "tiene granos"
jlüel:jlüel V-IV ; ! "vuela"
jlǘyan:jlǘyan V-IV ; ! "está quieto"
jmbok:jmbok V-IV ; ! "huele"
jmel:jmel V-IV ; ! "entra"
jmiük:jmiük V-IV ; ! "se cae; se despeña; baja"
jndok:jndok V-IV ; ! "sale humo"
jneaj:jneaj V-IV ; ! "sana"
jngot:jngot V-IV ; ! "llega"
jntsop:jntsop V-IV ; ! "germina"
joet:joet V-IV ; ! "llueve"
jomb:jomb V-IV ; ! "rema"
jpoet:jpoet V-IV ; ! "$orea"
jtean:jtean V-IV ; ! "se cae "
jtep:jtep V-IV ; ! "sube"
jtex:jtex V-IV ; ! "se pudre "
jtiür:jtiür V-IV ; ! "vomita"
jtoch:jtoch V-IV ; ! "tropieza"
jüik:jüik V-IV ; ! "aparece"
jün:jün V-IV ; ! "se cuece; hornea; madura; se descomponse "
jünch:jünch V-IV ; ! "gotea"
jüyjüy:jüyjüy V-IV ; ! "pasea"
jwik:jwik V-IV ; ! "late"
kants:kants V-IV ; ! "rojea"
kieel:kie%{ː%}l V-IV ; ! "se hinca"
kiy:kiy V-IV ; ! "demora"
kwal:kwal V-IV ; ! "concibe"
kwiür:kwiür V-IV ; ! "corre"
kwüüch:kwüüch V-IV ; ! "patea"
l:l V-IV ; ! "está"
latsam:latsam V-IV ; ! "está extiendo"
leamb:leamb V-IV ; ! "yéndose"
ley:ley V-IV ; ! "se ablanda"
lipiich:lipiich V-IV ; ! "relampaguea"
litlit:litlit V-IV ; ! "brilla"
liük:liük V-IV ; ! "viene"
mbeapmbeap:mbeap%>mbeap V-IV ; ! "tiembla de frío"
mbeat:mbeat V-IV ; ! "sube el precio; vende mucho"
meaats:meaats V-IV ; ! "tiene capacidad"
ndand:ndand V-IV ; ! "se endurece; aprende bien"
neorror:neorror V-IV ; ! "hace ruido fuerte"
ngün:ngün V-IV ; ! "se emborracha"
ngwiiüts:ngwiiüts V-IV ; ! "anochece"
ong:ong V-IV ; ! "da fruta"
paiich:paiich V-IV ; ! "insulta"
pat:pat V-IV ; ! "es bravo"
pateay:pateay V-IV ; ! "es pequeño; son pocos; disminuye"
pep:pep V-IV ; ! "tirar, hincha"
poniich:poniich V-IV ; ! "anubla"
rraj:rraj V-IV ; ! "alumbra"
somb:somb V-IV ; ! "se cubre"
süet:süet V-IV ; ! "predice"
süüd:sü%{ː%}d V-IV ; ! "se arrima"
tam:tam V-IV ; ! "sirve"
taxey:taxey V-IV ; ! "envejece"
terriür:terriür V-IV ; ! "retruena"
titit:titit V-IV ; ! "$amea mucho"
tow:tow V-IV ; ! "es del mismo tamaño"
tsoj:tsoj V-IV ; ! "chancea; pelea"
üet:üet V-IV ; ! "come "
üm:üm V-IV ; ! "pesa"
waag:waag V-IV ; ! "se seca"
xeeb:xe%{ː%}b V-IV ; ! "se baña"
xemem:xemem V-IV ; ! "anochece "
xeyay:xeyay V-IV ; ! "se aumenta"
xing:xing V-IV ; ! "da pena"
xip:xip V-IV ; ! "ponerse gordo; fermenta "
xix:xix V-IV ; ! "chi$a "
xix:xix V-IV ; ! "en$aquece"
xood:xo%{ː%}d V-IV ; ! "posa"

jkan:jkan V-IV ; ! "es de la misma edad"


LEXICON Interyecciones

amén:amén INTJ ; ! "amén"
ajaj:ajaj INTJ ; ! "sí"
aja:aja INTJ ; ! "sí"

LEXICON Símbolos

.%<sent%>:. # ;
..%<sent%>:.. # ;
...%<sent%>:... # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%¡%<sent%>:%¡ # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
%―%<guio%>:%― # ;
%–%<guio%>:%– # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%¿%<lquest%>:%¿ # ;
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
（%<lpar%>:（ # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
）%<rpar%>:） # ;
\%<sent%>:\ # ;
/%<sent%>:/ # ;
…%<sent%>:… # ;

LEXICON Digits

<[ %0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 ]+> NUM ;
<[ %0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 ]+ [ %, | %. ] [ %0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 ]+> NUM ;

